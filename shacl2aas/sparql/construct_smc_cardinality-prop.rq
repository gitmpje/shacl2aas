CONSTRUCT {
  # Submodel Element Collection (SMC)
  ?SMC_iri a aas:SubmodelElementCollection ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:type aasreft:GlobalReference ;
      aasref:keys [
        a aas:Key ;
        aaskey:type aaskeyt:GlobalReference ;
        aaskey:value ?PropertyStr ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?PropertyShape ;
    mas4ai:constructedByQuery "construct_smc_cardinality.rq" ;
  .
}
WHERE {
  {
    ?PropertyShape a sh:PropertyShape ;
      sh:maxCount ?maxCount ;
    .
    FILTER( ?maxCount > 1 )
  } UNION {
    ?PropertyShape a sh:PropertyShape .
    FILTER NOT EXISTS { ?PropertyShape sh:maxCount [] }
  }

  ?PropertyShape sh:path ?Property .

  BIND(strdt(str(?Property), xsd:string) AS ?PropertyStr)
  BIND(iri(concat( "http://mas4ai.eu/id/smc/template/", struuid() )) as ?SMC_iri)
}