CONSTRUCT {
  # Property
  ?Property_iri a aas:Property ;
    aasprop:valueType ?aas_datatype ;
    aassem:semanticId [
      a aas:Reference ;
      aasref:type aasreft:GlobalReference ;
      aasref:keys [
        a aas:Key ;
        aaskey:type aaskeyt:GlobalReference ;
        aaskey:value ?PropertyStr ;
      ] ;
    ] ;
    prov:wasDerivedFrom ?PropertyShape ;
    mas4ai:constructedByQuery "construct_property.rq" ;
  .
}
WHERE {
  ?PropertyShape a sh:PropertyShape ;
    sh:path ?Property ;
    sh:datatype ?datatype ;
  .

  FILTER(?datatype != rdf:langString) #filter out multi-language properties
  FILTER NOT EXISTS { ?PropertyShape sh:class [] } #filter out reference (object) properties

  BIND(strdt(str(?Property), xsd:string) AS ?PropertyStr)
  BIND(strafter(str(?datatype), str(xsd:)) AS ?datatypeName)
  BIND(iri(concat(str(aasdt:), ucase(substr(?datatypeName, 1, 1)), substr(?datatypeName, 2))) AS ?aas_datatype)
  BIND(iri(concat( "http://mas4ai.eu/id/property/template/", struuid() )) AS ?Property_iri)
}